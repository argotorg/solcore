import std;
import dispatch;

function caller() -> address {
  let res: word;
  assembly {
     res := caller()
  }
  return address(res);
}

function myrevert(msg: word) -> () {
       assembly { mstore(0, msg) revert(0, 32) }
}

function require(cond: bool, msg: word ) {
      if( !cond ) { myrevert(msg); }
}

contract MiniERC20 {
  reserved : word; // forge idiosyncrasies
  name : string;
  symbol : string;
  owner : address;
  decimals : uint256;
  totalSupply : uint256;
  balances : mapping(address,uint256);
  allowance : mapping(address, mapping(address, uint256));


  constructor(name_ : memory(string), symbol_ : memory(string), totalSupply_:uint256) {
    name = name_;
    symbol = symbol_;
    owner = caller();
    decimals = uint256(18);
    mint(totalSupply_);
  }


  function test() -> uint256 {
      approve(address(0), uint256(10));
      transferFrom(caller(), address(0), uint256(958));
      return getMyBalance();
  }
  function getName() -> memory(string) {
    return name;
  }


  function getMyBalance() -> uint256 {
    return balances[caller()];
  }


  function getMyAllowance() -> uint256 {
    return allowance[owner][address(0)];
  }

  function mint(amount:uint256) -> () {
    balances[owner] = Num.add(balances[owner], amount);
    totalSupply = Num.add(totalSupply, amount);
  }

  function transfer(dst : address, amt : uint256) -> bool {
      return transferFrom(caller(), dst, amt);
  }

  function transferFrom(src:address, dst:address, amt:uint256) -> bool {
     let msg_sender = caller();
     require( balances[src] >= amt /* "token/insufficient-balance" */
            , 0x746f6b656e2f696e73756666696369656e742d62616c616e6365
	    );

     if (src != msg_sender && allowance[src][msg_sender] != (Num.maxVal():uint256)) {
        require( allowance[src][msg_sender] >= amt /* "token/insufficient-allowance" */
	       , 0x746f6b656e2f696e73756666696369656e742d616c6c6f77616e6365
	       );
        allowance[src][msg_sender] -= amt;
     }
     balances[src] = balances[src] - amt;
     balances[dst] = balances[dst] + amt;
     // emit Transfer(src, dst, amt);
     return true;
  }

  function approve(usr: address, amt: uint256) -> bool {
      let msg_sender = caller();
      allowance[msg_sender][usr] = amt;
      // emit Approval(msg.sender, usr, amt);
      return true;

  }
