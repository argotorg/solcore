
/////// Construction
forall abs rep . class abs:Typedef(rep) {
    function rep(x:abs) -> rep;
    function abs(x:rep) -> abs;
}

instance word:Typedef(word) {
    function rep(x:word) -> word { return x; }
    function abs(x:word) -> word { return x; }
}

data uint = uint(word);

instance uint:Typedef(word) {
    function rep(x:uint) -> word {
        match x {
            | uint(y) => return y;
        }
    }
    function abs(x:word) -> uint {
        return uint(x);
    }
}

data memory(a) = memory(word);
data memoryRef(a) = memoryRef(word);
data Proxy(a) = Proxy;

forall a . instance memory(a):Typedef(word) {
    function rep(x:memory(a)) -> word {
        match x {
            | memory(y) => return y;
        }
    }
    function abs(x:word) -> memory(a) {
        return memory(x);
    }
}
forall a . instance memoryRef(a):Typedef(word) {
    function rep(x:memoryRef(a)) -> word {
        match x {
            | memoryRef(y) => return y;
        }
    }
    function abs(x:word) -> memoryRef(a) {
        return memoryRef(x);
    }
}

forall lhs rhs . class lhs:Assign(rhs) {
    function assign(l:lhs, r:rhs) -> ();
}

data ref(a) = ref(a);

forall a . instance ref(a):Assign(a) {
    function assign(l:ref(a), r:a) -> () {
        // builtin "stack store"
        return ();
    }
}

forall self . class self:MemoryType {
    function load(ptr:word) -> self;
    function store(ptr:word, value:self) -> ();
}

forall self . class self:MemorySize {
    function size(x:Proxy(self)) -> word;
}

instance word:MemoryType {
    function load(ptr:word) -> word {
        let r:word;
        assembly {
            r := mload(ptr);
        }
        return r;
    }
    function store(ptr:word, value:word) -> () {
        assembly {
            mstore(ptr, value)
        }
    }
}

instance uint:MemoryType {
    function load(ptr:word) -> uint {
       return Typedef.abs(MemoryType.load(ptr));
    }
    function store(ptr:word, value:uint) -> () {
        return MemoryType.store(ptr, Typedef.rep(value));
    }
}

forall a . a : MemoryType => instance memoryRef(a):Assign(a) {
    function assign(l:memoryRef(a), y:a) -> () {
        MemoryType.store(Typedef.rep(l), y);
    }
}



data MemberAccessProxy(a, field) = MemberAccessProxy(a, field);

forall a field .
function memberAccessD1(x:MemberAccessProxy(a, field)) -> a {
    match x {
        | MemberAccessProxy(y,z) => return y;
    }
}

forall self memberRefType . class self:LValueMemberAccess(memberRefType) {
    function memberAccess(x:self) -> memberRefType;
}

forall self memberValueType . class self:RValueMemberAccess(memberValueType) {
    function memberAccess(x:self) -> memberValueType;
}

// This is *a lot* of pragmas...
pragma no-coverage-condition StructField, LValueMemberAccess, RValueMemberAccess;
pragma no-patterson-condition LValueMemberAccess, RValueMemberAccess;
pragma no-bounded-variable-condition LValueMemberAccess, RValueMemberAccess;
forall self fieldType offsetType . class self:CStructField(fieldType, offsetType) {}
data StructField(structType, fieldSelector) = StructField(structType);

forall structType fieldSelector fieldType offsetType . StructField(structType, fieldSelector):CStructField(fieldType, offsetType), offsetType:MemorySize => instance MemberAccessProxy(memory(structType), fieldSelector):LValueMemberAccess(memoryRef(fieldType)) {
    function memberAccess(x:MemberAccessProxy(memory(structType), fieldSelector)) -> memoryRef(fieldType) {
        let ptr:word = Typedef.rep(memberAccessD1(x));
        let size:word = MemorySize.size(Proxy:Proxy(offsetType));
        assembly {
            ptr := add(ptr, size)
        }
        return memoryRef(Typedef.abs(ptr));
    }
}

instance ():MemorySize {
    function size(x:Proxy(())) -> word {
        return 0;
    }
}

instance word:MemorySize {
    function size(x:Proxy(word)) -> word {
        return 32;
    }
}


instance uint:MemorySize {
    function size(x:Proxy(uint)) -> word {
        return 32;
    }
}

forall a b . a:MemorySize, b:MemorySize => instance (a,b):MemorySize {
    function size(x:Proxy((a,b))) -> word {
        let a_sz:word = MemorySize.size(Proxy:Proxy(a));
        let b_sz:word = MemorySize.size(Proxy:Proxy(b));
        assembly {
            a_sz := add(a_sz, b_sz)
        }
        return a_sz;
    }
}

forall structType fieldSelector fieldType offsetType . StructField(structType, fieldSelector):CStructField(fieldType, offsetType), fieldType:MemoryType, offsetType:MemorySize => instance MemberAccessProxy(memory(structType), fieldSelector):RValueMemberAccess(fieldType) {
    function memberAccess(x:MemberAccessProxy(memory(structType), fieldSelector)) -> fieldType {
        let ptr:word = Typedef.rep(memberAccessD1(x));
        let size:word = MemorySize.size(Proxy:Proxy(offsetType));
        // BUG: Something wrong here? Complains about ptr not being word...
        assembly {
          ptr := add(ptr, size)
        }
        return MemoryType.load(Typedef.abs(ptr)):fieldType;
    }
}

////// Testing

// struct S { x:word; y:uint; z:word; }
data S = S(word, uint, word);
data x_sel = x_sel;
data y_sel = y_sel;
data z_sel = z_sel;

instance StructField(S, x_sel):CStructField(word, ()) {}
instance StructField(S, y_sel):CStructField(uint, word) {}
// BUG: This next one should really be the following, but that breaks weirdly:
// (I get a patterson condition violation on an invoke instance for g)
// instance StructField(S, z_sel):StructField(word, (word,uint)) {}
// So instead I use:
instance StructField(S, z_sel):CStructField(word, word) {}


function f() {
    let x:memory(word);
    let y:memory(word);
    // x = y
    Assign.assign(ref(x), y);
    /*
     * Idea in the above: to avoid overlapping instances,
     * we can desugar a simple identifier referring to a local variable on the lhs of an assignment to ref(x),
     * to be able to choose a disjoint assign instance.
     * Of course this needs special treatment during code generation,
     * on the other hand, stack assignments generally do...
     * Actually, even simpler might be just *not* to desugar assignments at all, if the lhs is just an identifier referring to a local variable and just directly take care of it when translating to core.
     */
}

function g() {
    let s:memory(S) = Typedef.abs(0x80);
    let y:word = 42;
    let z:uint = uint(42);
    // s.x = y
    Assign.assign(LValueMemberAccess.memberAccess(MemberAccessProxy(s, x_sel)), y);
    // s.y = 21
    Assign.assign(LValueMemberAccess.memberAccess(MemberAccessProxy(s, y_sel)), z);
    // s.z = y;
    Assign.assign(LValueMemberAccess.memberAccess(MemberAccessProxy(s, z_sel)), y);
    // y = s.x
    Assign.assign(ref(y), RValueMemberAccess.memberAccess(MemberAccessProxy(s, x_sel)));
    // s.z = s.x
    Assign.assign(LValueMemberAccess.memberAccess(MemberAccessProxy(s, z_sel)), RValueMemberAccess.memberAccess(MemberAccessProxy(s, x_sel)));
}
contract C {
    function main() {
        f();
        g();
    }
}
